//===== Cronus Script ======================================= 
//= Quest das Orelhas de Coelho
//===== By: ================================================== 
//= Matheus
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= Cronus GIT
//===== Description: ========================================= 
//= Quest para obter Orelhas de Coelho.
//===== Additional Comments: ================================= 
//= 1.1 using duplicate command
//= 1.2 Fixed NPC location, removed NPC dupes [Lupus]
//= 1.3 RE-Fixed exploit V_V, also cleared used VAR [Lupus]
//= 1.4 replaced BUNYBND with @BUNYBND [Lupus]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//	Changed @BUNYBAN back to BUNYBND, it should be this way.
//	Removed duplicates. NPC is ALberta-only.
//= 1.6 Cleaning. [Euphy]
//= 1.7 Traduzido e Otimizado. [Matheus]
//============================================================ 

alberta,26,229,0	script	Funcionário Kafra#bunny	83,{
	if (BUNYBND == 1) {
		mes "[Responsável pelo Festival]";
		mes "Eu sou responsável pelo ^529DFFFestival das ^FF0000Orelhas de Coelho^529DFF!!^000000";
		next;
		switch( select( "Participar do festival:Informações sobre o festival:Cancelar" ) ){
			case 1:
				mes "[Responsável pelo Festival]";					
				mes "Vamos ver se você trouxe todo os materiais necéssarios.";
				next;
				setarray .@Items[0],949,100,706,1,722,1,2213,1;
				for(set .@i,0; .@i<8; set .@i,.@i+2)
					if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
						mes "[Kafra Employee]";
						mes "Ah, me desculpe,";
						mes "mas você precisa me trazer pelo menos";
						mes .@Items[.@i+1]+" "+getitemname(.@Items[.@i])+((!.@i)?"s.":".");
						close;
					}
				mes "[Responsável pelo Festival]";
				mes "Puxa vida! Você trouxe todos os itens!";
				mes "Ok, eu vou fazer as suas Orelhas de Coelho.";
				mes "Aguarde só um minutinho.";
				next;
				for(set .@i,0; .@i<8; set .@i,.@i+2)
					if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
						mes "[Kafra Employee]";
						mes "Hm ? Me desculpe,";
						mes "mas eu não posso fazer isso agora";
						mes "Você precisa de";
						mes .@Items[.@i+1]+" "+getitemname(.@Items[.@i])+((!.@i)?"s":"");
						mes "Para eu lhe entregar as Orelhas de Coelho...";
						close;
					}
				for( set .@i, 0; .@i < 8; set .@i, .@i + 2 )
					delitem .@Items[.@i], .@Items[(.@i+1)];
				mes "[Responsável pelo Festival]";
				mes "Aqui está a sua Orelhas de Coelho!";
				getitem 2214,1; //Orelhas de Coelho
				set BUNYBND,0;
				next;
				mes "[Responsável pelo Festival]";
				mes "Obrigado por participar do festival! Espero que você goste das suas Orelhas de Coelho!";
				mes "Espero vê-lo novamente!";
				close;
				
			case 2:
				goto L_Info;
				
			case 3:
				close;
		}
	}
	mes "[Responsável pelo Festival]";
	mes "Festival das Orelhas de Coelho!";
	next;
	switch( select( "Participar do festival:Informações sobre o festival:Cancelar" ) ){
		case 1:
			set BUNYBND,1;
			mes "[Responsável pelo Festival]";
			mes "Obrigado pelo seu interesse no festival!";
			mes "Volte quando tiver todos os itens necessários para se fazer Orelhas de Coelho.";
			close;
		case 2:
			break;
		case 3:
			close;
	}
L_Info:
	mes "[Responsável pelo Festival]";
    mes "Para servi-lo cada vez melhor, nós da Corporação Kafra organizamos este Festival das Orelhas de Coelho.";
	next;
	mes "[Responsável pelo Festival]";				
	mes "Eu sou responsável pelo ^529DFFFestival das ^FF0000Orelhas de Coelho^529DFF!!^000000";
	mes "Nós faremos para você o fantástico item chamado ^FF0000Orelhas de Coelho^000000!";
	mes "(Patrocínio: Guilda dos Arqueiros de Payon)";
	next;
	mes "[Responsável pelo Festival]";
	mes "Basta que você nos traga os materiais necessários para a manufatura.";
	mes "As Orelhas de Coelho serão feitas na hora pelo Responsável pelo Festival.";
	mes "Os itens necessários são os seguintes:";
	next;
	mes "[Responsável pelo Festival]";
	mes "100 plumas";
	mes "1 Trevo de Quatro Folhas";
	mes "1 Pérola";
	mes "1 Orelhas de Gato";
	next;
	mes "[Responsável pelo Festival]";
	mes "Quando você tiver todos os itens, nós faremos uma Orelhas de Coelho para você.";
	mes "Boa Sorte!";
	close;
}
